define 

######
# CORD19
######

# Entities

journal sub entity, 
	has journal-name,
	plays publishing-journal; 

publication sub entity, 
	has paper-id,
	has title,
	has doi,
	has publish-time, 
	has volume, 
	has issn, 
	has pmid,
	plays mentioning,
	plays published-publication,
	plays authored-publication, 
	plays containing-paper, 
	plays referenced-paper,
	plays referencing-paper; 

paragraph sub entity,
	has section-name,
	has text, 
	has language,
	plays containing-span-paragraph, 
	plays contained-paper-paragraph,
	plays mentioning-paragraph,
	plays contained-patent-paragraph; 

span sub entity, 
	has start,
	has end,
	has text,
	plays referencing-paragraph,
	plays contained-span;

cite-span sub span;
ref-span sub span; 

person sub entity,
	has first-name,
	has middle-name,
	has last-name, 
	has published-name,
	has email,
	plays author,
	plays affiliated,
	plays patent-applicant,
	plays patent-owner,
	plays inventor; 

organisation sub entity, 
	has name, 
	plays affiliating,
	plays located, 
	plays locating,
	plays patent-owner,
	plays patent-applicant, 
	plays inventor; 
	laboratory sub organisation; 
	institution sub organisation;
	company sub organisation; 

geo-location sub entity,
	has name, 
	plays locating,
	plays located,
	plays discovering-location;
country sub geo-location,
	has country-name; 
settlement sub geo-location; 
region sub geo-location;

# Relations

authorship sub relation, 
	relates author, 
	relates authored-publication;

affiliateship sub relation, 
	relates affiliated,
	relates affiliating;

located-in sub relation, 
	relates locating, 
	relates located;

paper-paragraph-containment sub relation, 
	relates containing-paper, 
	relates contained-paper-paragraph; 

patent-paragraph-containment sub relation, 
	relates containing-patent, 
	relates contained-patent-paragraph; 

paragraph-span-containment sub relation,
	relates contained-span, 
	relates containing-span-paragraph;

mention sub relation, 
	has start, 
	has end,
	has source,	#CORD NER, SemMed etc 
	relates mentioning-paragraph, 
	relates mentioning, 
	relates mentioned-genes-relation;

bibliography-reference sub relation, 
	has ref-id,
	has venue, 
	has pages,
	relates referencing-paragraph,
	relates referencing-paper,
	relates referenced-paper;

publishing sub relation, 
	relates publishing-journal,
	relates published-publication; 

# Attributes

identifier sub attribute, value string; 
paper-id sub identifier;
title sub attribute, value string; 
section-name sub attribute, value string; 
text sub attribute, value string; 
start sub attribute, value string; 
end sub attribute, value string; 
name sub attribute, value string; 
first-name sub name; 
middle-name sub name; 
last-name sub name; 
country-name sub name;
published-name sub name;
email sub attribute, value string; 
ref-id sub identifier;
title sub attribute, value string; 
publish-time sub attribute, value string; 
venue sub attribute, value string; 
volume sub attribute, value string; 
issn sub attribute, value string; 
pages sub attribute, value string; 
pmid sub attribute, value string; 
language sub attribute, value string;
doi sub attribute, value string; 
source sub attribute, value string;
sentence-text sub attribute, value string;

### Rules: 
### 1: When text -> cite span -> bib reference -> paper then text -> paper 

######
### LENS 
######

patent sub entity,
	has publication-number, 
	has lens-id,
	has publication-date,
	has publication-year,
	has application-number,
	has application-date,
	has priority-numbers, 
	has earliest-priority-date,
	has title, 
	has URL,
	has patent-type,
	has cited-by-patent-count,
	has simple-family-size,
	has extended-family-size,
	has sequence-count,
	has cpc-classifications, 
	has IPCR-classifications,
	has US-classifications,
	has NPL-citation-count,
	has NPL-resolved-citation-count,
	has NPL-resolved-lens-id,
	has NPL-resolved-external-id,
	has NPL-citations,
	plays granted-patent,
	plays applied-patent,
	plays invented,
	plays owned-patent,
	plays containing-patent,
	plays simple-familiar-patent;

jurisdiction sub entity, 
	has name, 
	plays granting-jurisdiction;

## Relations

grantship sub relation, 
	relates granting-jurisdiction,
	relates granted-patent;

application sub relation,
	relates applied-patent,
	relates patent-applicant;

invention sub relation,
	relates invented,
	relates inventor;

ownership sub relation, 
	relates patent-owner,
	relates owned-patent;

simple-lens-family sub relation, 
	has family-id,
	relates simple-familiar-patent;

extended-lens-family sub relation, 
	has family-id,
	relates extended-familiar-patent;

publication-number sub attribute, value string;
lens-id sub identifier;
family-id sub identifier;
publication-date sub attribute, value string;
publication-year sub attribute, value string;
application-number sub identifier;
application-date sub attribute, value string;
priority-numbers sub attribute, value long;
earliest-priority-date sub attribute, value string;
URL sub attribute, value string;
patent-type sub attribute, value string;
cited-by-patent-count sub attribute, value string;
simple-family-size sub attribute, value string;
extended-family-size sub attribute, value string;
sequence-count sub attribute, value string;
cpc-classifications sub attribute, value string;
IPCR-classifications sub attribute, value string;
US-classifications sub attribute, value string;
NPL-citation-count sub attribute, value string;
NPL-resolved-citation-count sub attribute, value string;
NPL-resolved-lens-id sub attribute, value string;
NPL-resolved-external-id sub attribute, value string;
NPL-citations sub attribute, value string;


######
### Molecular Schema
######

substance sub entity; 

chemical sub substance,
	has chemical-name; 

variant sub entity,
	has snpId,
	plays located-variant;
chromosome sub entity, 
	has chromosome-name,
	plays locating-chromosome;

fully-formed-anatomical-structure sub entity;

cell sub fully-formed-anatomical-structure,
	has cell-name;

tissue sub fully-formed-anatomical-structure,
    has tissue-name,
    plays enhanced-tissue,
    plays expressed-tissue,
    plays tissue-context,
    plays associated-tissue;

gene sub fully-formed-anatomical-structure,
    has gene-symbol,
    has entrez-id,
    has ensembl-gene-stable-id,
    has ensembl-xref,
    has gene-name,
    plays expressing-gene,
    plays encoding-gene,
    plays transcribing-gene,
    plays mentioned,
    plays associated-gene,
    plays target-gene,
	plays associated-virus-gene,
	plays located-gene,
	plays inhibited,
	plays inhibiting,
	plays interacted,
	plays interacting,
	plays coexisting,
	plays comparing,
	plays compared,
	plays higher,
	plays lower,
	plays stimulating,
	plays stimulated,
	plays converting,
	plays converted,
	plays producing,
	plays produced,
	plays neg-coexisting,
	plays neg-inhibited,
	plays neg-inhibiting,
	plays neg-interacted,
	plays neg-interacting,
	plays neg-stimulating,
	plays neg-stimulated,
	plays neg-producing,
	plays neg-produced,
	plays neg-constituting,
	plays neg-constituted,
	plays similar,
	plays neg-similar,
	plays locating,
	plays located,
	plays constituting,
	plays constituted,
	plays neg-higher,
	plays neg-lower,
	plays neg-converting,
	plays neg-converted,
	plays disrupting,
	plays disrupted,
	plays augmenting,
	plays augmented,
	plays affecting,
	plays affected,
	plays associated,
	plays associating;
transcript sub fully-formed-anatomical-structure, 
	has ensembl-transcript-stable-id, 
	plays encoded-transcript,
	plays translating-transcript,
	plays mentioned;
protein sub chemical,
    has uniprot-id,
    has uniprot-entry-name,
    has uniprot-symbol,
    has uniprot-name,
    has ensembl-protein-stable-id, 
    has go-evidence-code,
    has go-taxon,
    has go-id,
    has function-description,
    plays modulated,
	plays activating-protein,
	plays activated-protein,
    plays translated-protein,
    plays associated-protein, 
    plays mentioned,
    plays encoded-protein,
    plays expressed-protein,
    plays interacting-protein,
    plays participating-protein,
    plays target-protein,
    plays associating,
    plays hosting-virus-protein;
pathological-function sub entity;
cell-function sub pathological-function,
	has cell-function-name;
disease sub pathological-function,
    has disease-name,
    has disease-id,
    has disease-type,
    plays mentioned,
	plays categorised-disease,
    plays associated-disease,
    plays affected-disease;
disease-class sub entity, 
	has class-name,
	plays associated-class;
drug sub entity,
    has drug-name,
    has chembl-id,
    has drug-claim-name,
    has drug-claim-source,
    plays interacting-drug,
    plays inhibitor,
    plays substrate,
    plays antagonist,
    plays blocker,
    plays therapeutic,
    plays interacting-drug,
    plays associated-drug,
    plays resulting-drug;
compound sub entity,
	plays participating-compound,
	plays modulator;
pathway sub entity,
    has pathway-name,
    has pathway-id,
    plays participated-pathway;
organism sub entity,
	has organism-name,
	plays associated-organism,
	plays hosting-organism;
virus sub organism, 
	has genbank-id, 
	has virus-name, 
	has identity-percentage,
	plays mentioned,
	plays hosted-virus,
	plays associated-virus,
	plays discovered-virus;

## Relations
transcription sub relation, 
	relates transcribing-gene,
	relates encoded-transcript;
translation sub relation, 
	relates translated-protein,
	relates translating-transcript;
protein-complex sub relation, 
	has complexportal-name,
	has complexportal-id,
	has complexportal-object-symbol,
	has go-id,
	has go-taxon,
	has pmid,
	has go-evidence-code,
	plays mentioned,
	relates associated-protein;
gene-protein-encoding sub relation,
	relates encoded-protein,
	relates encoding-gene;
protein-disease-association sub relation,
    relates associated-protein,
    relates associated-disease;
gene-disease-association sub relation,
	has disgenet-score,
    relates associated-gene,
    relates associated-disease;
gene-tissue-association sub relation,
	relates associated-tissue,
	relates associated-gene;
drug-protein-interaction sub relation,
    relates inhibitor,
    relates substrate,
    relates antagonist,
    relates blocker,
    relates interacting-drug,
    relates target-protein;
drug-gene-interaction sub relation,
	has interaction-type,
    relates target-gene,
    relates interacting-drug;
tissue-enhancement sub relation,
    relates enhanced-tissue,
    relates expressed-protein;
process-localisation sub relation,
    relates biomolecular-process,
    relates tissue-context;
protein-protein-interaction sub relation,
    relates interacting-protein,
    relates activating-protein,
    relates activated-protein,
    plays biomolecular-process;
drug-disease-association sub relation,
    relates affected-disease,
    relates therapeutic;
pathway-participation sub relation,
	relates participating-protein,
	relates participated-pathway;
disease-tissue-association sub relation,
    relates associated-disease,
    relates associated-tissue;
drug-tissue-association sub relation,
    relates associated-drug,
    relates associated-tissue;
drug-compound-combination sub relation, 
	relates participating-compound,
	relates resulting-drug;
modulation sub relation,
	relates modulator,
	relates modulated;
organism-association sub relation, 
	relates associated-organism,
	relates associating;
organism-virus-hosting sub relation,
	relates hosted-virus,
	relates hosting-organism;
protein-virus-association sub relation,
	relates associated-virus,
	relates hosting-virus-protein;
gene-virus-association sub relation, 
	relates associated-virus,
	relates associated-virus-gene;
discovery sub relation,
	relates discovered-virus,
	relates discovering-location;
chromosome-location sub relation,
	has start-location, 
	has end-location,
	relates located-variant,
	relates located-gene,
	relates locating-chromosome;
disease-class-association sub relation,
	relates categorised-disease,
	relates associated-class; 

expression sub relation, 
	has expression-value, 
	has expression-value-reliability,
	relates expressed-tissue,
	relates expressing-gene;

inhibition sub relation,
	relates inhibited,
	relates inhibiting,
	plays mentioned-genes-relation,
	has sentence-text;

interaction sub relation,
	relates interacted,
	relates interacting,
	plays mentioned-genes-relation,
	has sentence-text;

coexistance sub relation,
	relates coexisting,
	plays mentioned-genes-relation,
	has sentence-text;

comparison sub relation,
	relates compared,
	relates comparing,
	relates higher,
	relates lower,
	plays mentioned-genes-relation,
	has sentence-text;

stimulation sub relation,
	relates stimulated,
	relates stimulating,
	plays mentioned-genes-relation,
	has sentence-text;

conversion sub relation,
	relates converted,
	relates converting,
	plays mentioned-genes-relation,
	has sentence-text;

production sub relation,
	relates producing,
	relates produced,
	plays mentioned-genes-relation,
	has sentence-text;

 neg-coexistance sub relation,
	relates neg-coexisting,
	plays mentioned-genes-relation,
	has sentence-text;

 neg-inhibition sub relation,
	relates neg-inhibited,
	relates neg-inhibiting,
	plays mentioned-genes-relation,
	has sentence-text;

neg-interaction sub relation,
	relates neg-interacted,
	relates neg-interacting,
	plays mentioned-genes-relation,
	has sentence-text;

neg-stimulation sub relation,
	relates neg-stimulated,
	relates neg-stimulating,
	plays mentioned-genes-relation,
	has sentence-text;

neg-production sub relation,
	relates neg-produced,
	relates neg-producing,
	plays mentioned-genes-relation,
	has sentence-text;

neg-constitution sub relation,
	relates neg-constituted,
	relates neg-constituting,
	plays mentioned-genes-relation,
	has sentence-text;

similarity sub relation,
	relates similar,
	plays mentioned-genes-relation,
	has sentence-text;

neg-similarity sub relation,
	relates neg-similar,
	plays mentioned-genes-relation,
	has sentence-text;

location sub relation,
	relates located,
	relates locating,
	plays mentioned-genes-relation,
	has sentence-text;

constitution sub relation,
	relates constituted,
	relates constituting,
	plays mentioned-genes-relation,
	has sentence-text;

neg-comparison sub relation,
	relates neg-higher,
	relates neg-lower,
	plays mentioned-genes-relation,
	has sentence-text;

neg-conversion sub relation,
	relates neg-converted,
	relates neg-converting,
	plays mentioned-genes-relation,
	has sentence-text;

disruption sub relation,
	relates disrupted,
	relates disrupting,
	plays mentioned-genes-relation,
	has sentence-text;

augmentation sub relation,
	relates augmented,
	relates augmenting,
	plays mentioned-genes-relation,
	has sentence-text;

affection sub relation,
	relates affected,
	relates affecting,
	plays mentioned-genes-relation,
	has sentence-text;

association sub relation,
	relates associated,
	relates associating,
	plays mentioned-genes-relation,
	has sentence-text;

## Attributes
# Name attributes: 
	chromosome-name sub name;
	class-name sub name; 
	disease-name sub name;
	disease-type sub name;
	database-name sub name;
	uniprot-name sub name;
	gene-symbol sub name;
	ensembl-xref sub name; 
	uniprot-symbol sub name; 
	complexportal-name sub name;
	complexportal-object-symbol sub name;
	gene-name sub name;
	protein-name sub name;
	journal-name sub name;
	cell-function-name sub name;
	cell-name sub name;
	chemical-name sub name;
	uniprot-entry-name sub name;
	pathway-name sub name;
	tissue-name sub name;
	drug-name sub name;
	organism-name sub name;
	virus-name sub name; 
	drug-claim-name sub name;

identifier sub attribute, value string;
	complex-id sub identifier;
		complexportal-id sub complex-id;
	gene-id sub identifier;
		ensembl-gene-stable-id sub gene-id;
		entrez-id sub gene-id;
	protein-id sub identifier;
		uniprot-id sub protein-id;
		ensembl-protein-stable-id sub protein-id;
		uniprot-id sub protein-id;
		ensembl-id sub protein-id;
	transcript-id sub identifier;
		ensembl-transcript-stable-id sub transcript-id;
	go-id sub identifier;
	disease-id sub identifier;
	genbank-id sub identifier;

	GEOStudy-id sub identifier;
	GEOComparison-id sub identifier;
	pathway-id sub identifier;
	chembl-id sub identifier;
	snpId sub identifier;



expression-value sub attribute, value string; 
expression-value-reliability sub attribute, value string; 
drug-claim-source sub attribute, value string;
identity-percentage sub attribute, value string;
go-evidence-code sub attribute, value string; 
go-taxon sub attribute, value long; 
function-description sub attribute, value string;
start-location sub attribute, value long;
end-location sub attribute, value long;
disgenet-score sub attribute, value double;
interaction-type sub attribute, value string;

### Rules


gene-disease-association-and-gene-protein-encoding-protein-disease-association sub rule,
when {
    $g isa gene;
    $pr isa protein;
    $di isa disease;
    $r1 (associated-disease: $di, associated-gene: $g) isa gene-disease-association;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
}, then {
    (associated-protein: $pr, associated-disease: $di) isa protein-disease-association;
};

gene-tissue-association-gene-protein-encoding-implies-tissue-enhancement sub rule,
when {
    $g isa gene;
    $pr isa protein;
    $ti isa tissue;
    $r1 (associated-tissue: $ti, associated-gene: $g) isa gene-tissue-association;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
}, then {
    (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
};

drug-gene-interaction-and-gene-protein-encoding-implies-drug-protein-interaction sub rule,
when {
    $g isa gene;
    $pr isa protein;
    $dr isa drug;
    $r1 ($dr, target-gene: $g) isa drug-gene-interaction;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
}, then {
    (target-protein: $pr, inhibitor: $dr) isa drug-protein-interaction;
};

protein-disease-association-and-tissue-enhancement-implies-disease-tissue-association sub rule,
when {
   $di isa disease;
   $pr isa protein;
   $ti isa tissue;
   $r1 (associated-disease: $di, associated-protein: $pr) isa protein-disease-association;
   $r2 (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
}, then {
   (associated-disease: $di, associated-tissue: $ti) isa disease-tissue-association;
};

drug-gene-interaction-and-gene-tissue-association-implies-drug-tissue-association sub rule,
when {
   $dr isa drug;
   $ti isa tissue;
   $ge isa gene;
   $r1 (interacting-drug: $dr, target-gene: $ge) isa drug-gene-interaction;
   $r2 (associated-gene: $ge, associated-tissue: $ti) isa gene-tissue-association;
}, then {
   (associated-drug: $dr, associated-tissue: $ti) isa drug-tissue-association;
};

virus-gene-then-virus-protein sub rule,
when {
	$g isa gene; 
	$v isa virus;
	$p isa protein; 
	$1 (associated-virus-gene: $g, associated-virus: $v) isa gene-virus-association;
	$2 (encoding-gene: $g, encoded-protein: $p) isa gene-protein-encoding; 
}, then {
	(hosting-virus-protein: $p, associated-virus: $v) isa protein-virus-association;
};

gene-protein-publication sub rule,
when {
	$p isa protein; 
	$g isa gene; 
	$pu isa publication; 
	$1 (encoding-gene: $g, encoded-protein: $p) isa gene-protein-encoding; 
	$2 (mentioned: $g, mentioning: $pu) isa mention; 
}, then {
	(mentioned: $p, mentioning: $pu) isa mention; 
};